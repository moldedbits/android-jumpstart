apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'checkstyle'


preBuild.dependsOn 'checkstyle'
check.dependsOn 'checkstyle'

// auto increment version stuff
import com.moldedbits.android.MBIncrementVersionTask

task incrementVersionTask(type: MBIncrementVersionTask)
tasks.whenTaskAdded { task ->
    if (task.name == 'generateReleaseBuildConfig') {
        task.dependsOn 'incrementVersionTask'
    }
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/quality/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/test/java/com/moldedbits/android/**'

    classpath = files()

    // set to false if you want to consider warning as error
    ignoreFailures = false
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    final API_URL_DEV = "https://bookstore-mb.herokuapp.com/";
    final API_URL_PROD = "https://bookstore-mb.herokuapp.com/";

    defaultConfig {
        applicationId "com.moldedbits.android"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion

        versionCode incrementVersionTask.getCurrentVersionCode()
        versionName incrementVersionTask.getCurrentVersionName()

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        // setting custom name for apk
        project.ext.set("archivesBaseName", "AndroidJumpstart-" + defaultConfig.versionName
                + "-vc" + defaultConfig.versionCode);
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    signingConfigs {
        // todo change as per app config
        debugProd {
            storeFile file("../jumpstart.keystore")
            storePassword "moldedbits"
            keyAlias "jumpstart"
            keyPassword "moldedbits"
        }
        release {
            storeFile file("../jumpstart.keystore")
            storePassword "moldedbits"
            keyAlias "jumpstart"
            keyPassword "moldedbits"
        }
        releaseProd {
            storeFile file("../jumpstart.keystore")
            storePassword "moldedbits"
            keyAlias "jumpstart"
            keyPassword "moldedbits"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
            buildConfigField 'String', 'API_URL', "\"" + API_URL_DEV + "\""
        }

        debugProd {
            applicationIdSuffix '.debug'
            minifyEnabled false
            buildConfigField 'String', 'API_URL', "\"" + API_URL_PROD + "\""
            signingConfig signingConfigs.debugProd
        }

        release {
            applicationIdSuffix '.release'
            buildConfigField 'String', 'API_URL', "\"" + API_URL_DEV + "\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        releaseProd {
            buildConfigField 'String', 'API_URL', "\"" + API_URL_PROD + "\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseProd
        }
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings false
        // if true, emit full/absolute paths to files with errors (true by default)
        //absolutePaths true
        // if true, check all issues, including those that are off by default
        checkAllWarnings true
        // if true, treat all warnings as errors
        warningsAsErrors false
        // turn off checking the given issue id's
        disable 'TypographyFractions', 'TypographyQuotes'
        // turn on the given issue id's
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        // check *only* the given issue id's
        //check 'NewApi', 'InlinedApi'
        // if true, don't include source code lines in the error output
        noLines false
        // if true, show all locations for an error, do not truncate lists, etc.
        showAll true
        // Fallback lint configuration (default severities, etc.)
        lintConfig file("default-lint.xml")
        // if true, generate a text report of issues (false by default)
        textReport false
        // location to write the output; can be a file or 'stdout'
        textOutput 'stdout'
        // if true, generate an XML report for use by for example Jenkins
        xmlReport false
        // file to write report to (if not specified, defaults to lint-results.xml)
        xmlOutput file("build/reports/lint/lint-report.xml")
        // if true, generate an HTML report (with issue explanations, sourcecode, etc)
        htmlReport true
        // optional path to report (default will be lint-results.html in the builddir)
        htmlOutput file("build/reports/lint/lint-report.html")
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // App's dependencies, including test
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.android.support.test.espresso:espresso-idling-resource:$rootProject.espressoVersion"
    compile "com.google.guava:guava:$rootProject.guavaVersion"
    compile "io.reactivex.rxjava2:rxjava:$rootProject.rxjavaVersion"
    compile "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroidVersion"
    compile "com.squareup.sqlbrite:sqlbrite:$rootProject.sqlbriteVersion"

    // Use Retrofit for API interaction
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.loggingInterceptorVersion"

    // Lombok
    // https://projectlombok.org/setup/android.html
    provided "org.projectlombok:lombok:$rootProject.lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$rootProject.lombokVersion"

    // Timber for logging
    compile "com.jakewharton.timber:timber:$rootProject.timberVersion"

    // Butterknife for view injection
    compile "com.jakewharton:butterknife:$rootProject.butterknifeVersion"

    // Dagger 2
    // Dagger 2 dependencies
    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    provided 'org.glassfish:javax.annotation:10.0-b28'

    // Dependencies for local unit tests
    testCompile "junit:junit:$rootProject.ext.junitVersion"
    testCompile "org.mockito:mockito-all:$rootProject.ext.mockitoVersion"
    testCompile "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"

    // Android Testing Support Library's runner and rules
    androidTestCompile "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestCompile "com.android.support.test:rules:$rootProject.ext.runnerVersion"

    // Dependencies for Android unit tests
    androidTestCompile "junit:junit:$rootProject.ext.junitVersion"
    androidTestCompile "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"

    // Resolve conflicts between main and test APK:
    androidTestCompile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support:design:$rootProject.supportLibraryVersion"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'
}
repositories {
    mavenCentral()
}